/* auditgn1.p */

DEFINE SHARED STREAM s_audit_gen.
DEFINE STREAM s_audit_prg.

FOR EACH dictdb._file WHERE NOT dictdb._file._file-name BEGINS "_" 
                        AND NOT dictdb._file._file-name BEGINS "SYS" NO-LOCK:
  PUT STREAM s_audit_gen UNFORMATTED
    "WHEN ~"" dictdb._file._file-name "~" THEN" SKIP
    "RUN auditprg/" + dictdb._file._file-name ".p (ttbl_header.v_action)." SKIP.
  OUTPUT STREAM s_audit_prg TO VALUE("auditprg/" + dictdb._file._file-name + ".p").
  PUT STREAM s_audit_prg UNFORMATTED
    "/* " dictdb._file._file-name ".p - generated "
    TODAY FORMAT "99/99/9999" " - " STRING(TIME,"HH:MM:SS") " by "
    USERID("NOSWEAT") " */" SKIP(1)
    "/* this program was generated by auditgn1.p and should not be modified */" SKIP
    "/* modifications should be made to the auto generation program         */" SKIP(1)
    "DEFINE INPUT PARAMETER action AS CHARACTER NO-UNDO." SKIP(1)
    "DEFINE VARIABLE i AS INTEGER NO-UNDO." SKIP(1)
    "~{methods/defines/audit.i}" SKIP
    "FIND FIRST ttbl_header." SKIP(1)
    "DEFINE WORK-TABLE ttbl_" dictdb._file._file-name "1 NO-UNDO LIKE "
    dictdb._file._file-name "." SKIP
    "DEFINE WORK-TABLE ttbl_" dictdb._file._file-name "2 NO-UNDO LIKE "
    dictdb._file._file-name "." SKIP(1)
    "CREATE ttbl_" dictdb._file._file-name "1." SKIP
    "IMPORT STREAM s_audit_file ttbl_" dictdb._file._file-name "1." SKIP
    "CREATE ttbl_" dictdb._file._file-name "2." SKIP
    "IF action = ~"UPDATE~" THEN" SKIP
    "IMPORT STREAM s_audit_file ttbl_" dictdb._file._file-name "2." SKIP(1)
    "PUT STREAM s-audit-rpt UNFORMATTED" SKIP
    "  ~"Action    User ID/(User Name)            DB.Table/(Database)  Date       Time~" SKIP" SKIP
    "  ~"--------- ------------------------------ -------------------- ---------- --------~" SKIP" SKIP
    "  ttbl_header.v_action AT 1" SKIP
    "  ttbl_header.v_userid AT 11" SKIP
    "  ttbl_header.v_file AT 42" SKIP
    "  ttbl_header.v_date FORMAT ~"99/99/9999~" AT 63" SKIP
    "  STRING(ttbl_header.v_time,~"HH:MM:SS~") AT 74." SKIP
    "FIND users WHERE users.user_id = v_userid NO-LOCK NO-ERROR." SKIP
    "IF AVAILABLE users THEN" SKIP
    "PUT STREAM s-audit-rpt UNFORMATTED ~"(~" AT 10 users.user_name ~")~"." SKIP
    "ELSE" SKIP
    "PUT STREAM s-audit-rpt UNFORMATTED ~"(user record does not exist)~" AT 10." SKIP
    "PUT STREAM s-audit-rpt UNFORMATTED" SKIP
    "  ~"(~" AT 41 ttbl_header.v_db ~")~" SKIP(1)" SKIP
    "  ~"Label: Value (database field-name)~" AT 3." SKIP
    "IF action = ~"UPDATE~" THEN" SKIP
    "PUT STREAM s-audit-rpt UNFORMATTED ~" ( * - denotes field value change )~"." SKIP
    "PUT STREAM s-audit-rpt UNFORMATTED FILL(~"-~",75) AT 3." SKIP.
  FOR EACH dictdb._field OF dictdb._file
      WHERE dictdb._field._field-name NE "rec_key" NO-LOCK:
    IF dictdb._field._extent NE 0 THEN
    PUT STREAM s_audit_prg UNFORMATTED
      "DO i = 1 TO " dictdb._field._extent ":" SKIP.
    PUT STREAM s_audit_prg UNFORMATTED
      "IF NOT v_field_changed OR ttbl_" dictdb._file._file-name "1." dictdb._field._field-name.
    IF dictdb._field._extent NE 0 THEN
    PUT STREAM s_audit_prg UNFORMATTED "[i]".
    PUT STREAM s_audit_prg UNFORMATTED
      " NE ttbl_" dictdb._file._file-name "2." dictdb._field._field-name.
    IF dictdb._field._extent NE 0 THEN
    PUT STREAM s_audit_prg UNFORMATTED "[i]".
    PUT STREAM s_audit_prg UNFORMATTED
      " OR action NE ~"UPDATE~" THEN" SKIP
      "DO:" SKIP
      "  PUT STREAM s-audit-rpt UNFORMATTED ~"" dictdb._field._label.
    IF dictdb._field._extent NE 0 THEN
    PUT STREAM s_audit_prg UNFORMATTED "[~" AT 3 i ~"]: ~" ".
    ELSE
    PUT STREAM s_audit_prg UNFORMATTED ": ~" AT 3 ".
    PUT STREAM s_audit_prg UNFORMATTED
      "ttbl_" dictdb._file._file-name "1." dictdb._field._field-name.
    IF dictdb._field._extent NE 0 THEN
    PUT STREAM s_audit_prg UNFORMATTED "[i]".
    IF dictdb._field._data-type NE "character" THEN
    PUT STREAM s_audit_prg UNFORMATTED " FORMAT ~"" dictdb._field._format "~"".
    PUT STREAM s_audit_prg UNFORMATTED
      " ~" (" dictdb._field._field-name.
    IF dictdb._field._extent NE 0 THEN
    PUT STREAM s_audit_prg UNFORMATTED "[~" i ~"]".
    PUT STREAM s_audit_prg UNFORMATTED
      ")~"." SKIP
      "  IF action = ~"UPDATE~" THEN" SKIP
      "  DO:" SKIP
      "    IF ttbl_" dictdb._file._file-name "1." dictdb._field._field-name.
    IF dictdb._field._extent NE 0 THEN
    PUT STREAM s_audit_prg UNFORMATTED "[i]".
    PUT STREAM s_audit_prg UNFORMATTED
      " NE ttbl_"
      dictdb._file._file-name "2." dictdb._field._field-name.
    IF dictdb._field._extent NE 0 THEN
    PUT STREAM s_audit_prg UNFORMATTED "[i]".
    PUT STREAM s_audit_prg UNFORMATTED
      " THEN" SKIP
      "    PUT STREAM s-audit-rpt UNFORMATTED ~"*~" AT 1." SKIP
      "    PUT STREAM s-audit-rpt UNFORMATTED ~"" dictdb._field._label.
    IF dictdb._field._extent NE 0 THEN
    PUT STREAM s_audit_prg UNFORMATTED "[~" AT 3 i ~"]: ~" ".
    ELSE
    PUT STREAM s_audit_prg UNFORMATTED ": ~" AT 3 ".
    PUT STREAM s_audit_prg UNFORMATTED
      "ttbl_" dictdb._file._file-name "2." dictdb._field._field-name.
    IF dictdb._field._extent NE 0 THEN
    PUT STREAM s_audit_prg UNFORMATTED "[i]".
    IF dictdb._field._data-type NE "character" THEN
    PUT STREAM s_audit_prg UNFORMATTED " FORMAT ~"" dictdb._field._format "~"".
    PUT STREAM s_audit_prg UNFORMATTED "." SKIP
      "  END." SKIP
      "END." SKIP.
    IF dictdb._field._extent NE 0 THEN
    PUT STREAM s_audit_prg UNFORMATTED "END." SKIP.
  END.
  IF NOT CAN-DO('config,mfdata',dictdb._file._file-name) THEN
  DO:
    FIND dictdb._index WHERE RECID(dictdb._index) = dictdb._file._prime-index NO-LOCK.
    IF CAN-FIND(FIRST dictdb._index-field WHERE dictdb._index-field._index-recid = RECID(dictdb._index)) THEN
    PUT STREAM s_audit_prg UNFORMATTED
      "IF v_show_index THEN" SKIP
      "DO:" SKIP
      "  PUT STREAM s-audit-rpt UNFORMATTED ~"Index Fields - Label: Value (database field-name)~" AT 5." SKIP.
    FOR EACH dictdb._index-field
        WHERE dictdb._index-field._index-recid = RECID(dictdb._index) NO-LOCK:
      FIND dictdb._field
          WHERE RECID(dictdb._field) = dictdb._index-field._field-recid NO-LOCK.
      PUT STREAM s_audit_prg UNFORMATTED
        "  PUT STREAM s-audit-rpt UNFORMATTED ~"" dictdb._field._label ": ~" AT 7." SKIP
        "  IF action = ~"UPDATE~" THEN" SKIP
        "  PUT STREAM s-audit-rpt UNFORMATTED ttbl_" dictdb._file._file-name "2." dictdb._field._field-name.
      IF dictdb._field._data-type NE "character" THEN
      PUT STREAM s_audit_prg UNFORMATTED " FORMAT ~"" dictdb._field._format "~"".
      PUT STREAM s_audit_prg UNFORMATTED " ~" (" dictdb._field._field-name ")~"." SKIP.
      PUT STREAM s_audit_prg UNFORMATTED "  ELSE" SKIP
        "  PUT STREAM s-audit-rpt UNFORMATTED ttbl_" dictdb._file._file-name "1." dictdb._field._field-name.
      IF dictdb._field._data-type NE "character" THEN
      PUT STREAM s_audit_prg UNFORMATTED " FORMAT ~"" dictdb._field._format "~"".
      PUT STREAM s_audit_prg UNFORMATTED " ~" (" dictdb._field._field-name ")~"." SKIP.
    END.
    IF CAN-FIND(FIRST dictdb._index-field WHERE dictdb._index-field._index-recid = RECID(dictdb._index)) THEN
    PUT STREAM s_audit_prg UNFORMATTED "END." SKIP.
  END.
  PUT STREAM s_audit_prg UNFORMATTED "PUT STREAM s-audit-rpt UNFORMATTED SKIP(1)." SKIP.
  OUTPUT STREAM s_audit_prg CLOSE.
END.
