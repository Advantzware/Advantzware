/**********************************************************************
 * Copyright (C) 2006-2015 by Consultingwerk Ltd. ("CW") -            *
 * www.consultingwerk.de and other contributors as listed             *
 * below.  All Rights Reserved.                                       *
 *                                                                    *
 *  Software is distributed on an "AS IS", WITHOUT WARRANTY OF ANY    *
 *   KIND, either express or implied.                                 *
 *                                                                    *
 *  Contributors:                                                     *
 *                                                                    *
 **********************************************************************/
/*------------------------------------------------------------------------
    File        : ProparseNodeTypes
    Purpose     : Enumeration of Proparse NodeTypes
    Syntax      : 
    Description : 
    Author(s)   : Mike Fechner / Consultingwerk Ltd.
    Created     : Thu Jul 23 07:04:36 CEST 2015
    Notes       : Properties prefixed with N_ to avoid conflicts with ABL
                  keywords
  ----------------------------------------------------------------------*/

ROUTINE-LEVEL ON ERROR UNDO, THROW.

USING Consultingwerk.Studio.Proparse.NodeTypes.* FROM PROPATH .  
USING Progress.Lang.*                            FROM PROPATH .

{Consultingwerk/products.i}

CLASS Consultingwerk.Studio.Proparse.NodeTypes.ProparseNodeTypes: 

    {Consultingwerk/Studio/Proparse/NodeTypes/ttNodeTypes.i &ACCESS=STATIC}
    
@PROPERTIES@
    /*------------------------------------------------------------------------------
        Purpose: Private default constructor for the ProparseNodeTypes class 
        Notes:   Disallow instance creation                                                                      
    ------------------------------------------------------------------------------*/
    CONSTRUCTOR PRIVATE ProparseNodeTypes ():
        SUPER ().
        
    END CONSTRUCTOR.
    
    /*------------------------------------------------------------------------------
        Purpose: Returns the ABL Keyword for a given proparse Node 
        Notes:   
        @param pcNodeType The Proparse Node type
        @return The ABL keyword
    ------------------------------------------------------------------------------*/
    METHOD PUBLIC STATIC CHARACTER AblKeyword (pcNodeType AS CHARACTER):
        
        IF NOT CAN-FIND (FIRST ttNodeTypes) THEN 
            ProparseNodeTypes:ImportNodeTypes () .
        
        FIND ttNodeTypes WHERE ttNodeTypes.NodeType = pcNodeType .
        
        RETURN UPPER (ttNodeTypes.AblKeyWord) .
        
        CATCH err AS Progress.Lang.Error:
            UNDO, THROW NEW ProparseNodeTypesException (err, "Invalid proparse node type."{&TRAN}, 0) . 
        END CATCH.

    END METHOD .

    /*------------------------------------------------------------------------------
        Purpose: Returns the minimum number of characters the ABL keyword can be 
                 abbreviated with 
        Notes:   
        @param pcNodeType The Proparse Node type
        @return The minimum number of characters the ABL keyword can be abbreviated with
    ------------------------------------------------------------------------------*/
    METHOD PUBLIC STATIC INTEGER MinAbbreviated (pcNodeType AS CHARACTER):
        
        IF NOT CAN-FIND (FIRST ttNodeTypes) THEN 
            ProparseNodeTypes:ImportNodeTypes () .
        
        FIND ttNodeTypes WHERE ttNodeTypes.NodeType = pcNodeType .
        
        RETURN ttNodeTypes.MinAbbreviated .
        
        CATCH err AS Progress.Lang.Error:
            UNDO, THROW NEW ProparseNodeTypesException (err, "Invalid proparse node type."{&TRAN}, 0) . 
        END CATCH.

    END METHOD .

    /*------------------------------------------------------------------------------
        Purpose: Returns the proparse node type for a given ABL keyword 
        Notes:   
        @param pcAblKeyword The ABL keyword
        @return The propery node type
    ------------------------------------------------------------------------------*/
    METHOD PUBLIC STATIC CHARACTER NodeType (pcAblKeyword AS CHARACTER):
        
        IF NOT CAN-FIND (FIRST ttNodeTypes) THEN 
            ProparseNodeTypes:ImportNodeTypes () .
        
        FIND ttNodeTypes WHERE ttNodeTypes.AblKeyWord = pcAblKeyword .
        
        RETURN ttNodeTypes.NodeType . 
        
        CATCH err AS Progress.Lang.Error:
            UNDO, THROW NEW ProparseNodeTypesException (err, "Invalid proparse node type."{&TRAN}, 0) . 
        END CATCH.

    END METHOD .

    /*------------------------------------------------------------------------------
        Purpose: Imports the NodeTypes from the file Consultingwerk/Studio/Proparse/NodeTypes/nodetypes.xml
        Notes:   
    ------------------------------------------------------------------------------*/
    METHOD PROTECTED STATIC VOID ImportNodeTypes ():
        
        TEMP-TABLE ttNodeTypes:READ-XML ("FILE":U, 
                                         "Consultingwerk/Studio/Proparse/NodeTypes/nodetypes.xml":U,
                                         ?, ?, ?) .
        
    END METHOD .

END CLASS.
