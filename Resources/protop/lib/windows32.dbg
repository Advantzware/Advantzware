        1   /* lib/windows32.p
        2    *
        3    * windows kernel calls
        4    *
        5    * use this when _progres is a 32 bit executable
        6    *
        7    */
        8   
        9   session:add-super-procedure( this-procedure ).
       10   
       11   return.
       12   
       13   
       14   PROCEDURE CreateProcessA EXTERNAL "kernel32.dll":
       15     DEFINE INPUT  PARAMETER lpApplicationName    AS LONG.
       16     DEFINE INPUT  PARAMETER lpCommandline        AS CHAR.
       17     DEFINE INPUT  PARAMETER lpProcessAttributes  AS LONG.
       18     DEFINE INPUT  PARAMETER lpThreadAttributes   AS LONG.
       19     DEFINE INPUT  PARAMETER bInheritHandles      AS LONG.
       20     DEFINE INPUT  PARAMETER dCreationFlags       AS LONG.
       21     DEFINE INPUT  PARAMETER lpEnvironment        AS LONG.
       22     DEFINE INPUT  PARAMETER lpCurrentDirectory   AS LONG.
       23     DEFINE INPUT  PARAMETER lpStartupInfo        AS LONG.
       24     DEFINE INPUT  PARAMETER lpProcessInformation AS LONG.
       25     DEFINE RETURN PARAMETER bResult              AS LONG.
       26   end procedure.
       27   
       28   procedure OpenProcess external "kernel32.dll" :
       29     define input  parameter desiredAccess as long.
       30     define input  parameter inheritFlag   as long.
       31     define input  parameter pid           as long.
       32     define return parameter procHdl       as long.
       33   end procedure.
       34   
       35   procedure TerminateProcess external "kernel32.dll" :
       36     define input  parameter hProcess  as long.
       37     define input  parameter uExitCode as long.
       38     define return parameter retval    as long.
       39   end procedure.
       40   
       41   procedure CloseHandle external "kernel32.dll":
       42     define input  parameter procHdl  as long.
       43     define return parameter statcode as long.
       44   end.
       45   
       46   
       47   procedure spawn:
       48   
       49     DEFINE INPUT  PARAMETER CommandLine as character NO-UNDO.
       50     DEFINE INPUT  PARAMETER WorkingDir  as character NO-UNDO.
       51     DEFINE OUTPUT PARAMETER PID         AS INTEGER   NO-UNDO.
       52   
       53     define variable wShowWindow   AS INTEGER NO-UNDO INITIAL 0.
       54     define variable bResult       AS INTEGER NO-UNDO.
       55     define variable ReturnValue   AS INTEGER NO-UNDO.
       56     define variable lpStartupInfo AS MEMPTR  NO-UNDO.
       57   
       58     SET-SIZE ( lpStartupInfo )     = 68.
       59   
       60     PUT-LONG ( lpStartupInfo, 1 )  = 68.
       61     PUT-LONG ( lpStartupInfo, 45 ) = 1. /* = STARTF_USESHOWWINDOW */
       62   
       63     PUT-SHORT( lpStartupInfo, 49 ) = wShowWindow.
       64   
       65     define variable lpProcessInformation AS MEMPTR.
       66     SET-SIZE( lpProcessInformation )   = 16.
       67   
       68     define variable lpWorkingDirectory AS MEMPTR.
       69     IF WorkingDir NE "" THEN DO:
       70       SET-SIZE( lpWorkingDirectory )     = 256.
       71       PUT-STRING( lpWorkingDirectory, 1 ) = WorkingDir.
       72     END.
       73   
       74     RUN CreateProcessA(
       75       0,
       76       CommandLine,
       77       0,
       78       0,
       79       0,
       80       0,
       81       0,
       82       ( IF WorkingDir = "" THEN 0 ELSE GET-POINTER-VALUE( lpWorkingDirectory )),
       83       GET-POINTER-VALUE( lpStartupInfo ),
       84       GET-POINTER-VALUE( lpProcessInformation ),
       85       OUTPUT bResult
       86     ).
       87   
       88     IF bResult <> 0 THEN     /* release kernel-objects hProcess and hThread: */
       89       do:
       90   
       91         PID = GET-LONG( lpProcessInformation, 9 ).
       92         RUN CloseHandle ( input GET-LONG( lpProcessInformation, 1 ), OUTPUT ReturnValue ).
       93         RUN CloseHandle ( input GET-LONG( lpProcessInformation, 5 ), OUTPUT ReturnValue ).
       94   
       95       end.
       96   
       97     SET-SIZE( lpStartupInfo )        = 0.
       98     SET-SIZE( lpProcessInformation ) = 0.
       99     SET-SIZE( lpWorkingDirectory )   = 0.
      100   
      101   END PROCEDURE.
      102   
      103   
      104   procedure kill:
      105   
      106     define input parameter PID as integer no-undo.
      107     define variable processHandle as integer no-undo.
      108     define variable returnValue   as integer no-undo.
      109   
      110     run OpenProcess ( 1, 0, PID, OUTPUT ProcessHandle ).
      111   
      112     if processHandle NE 0 then
      113       do:
      114         run TerminateProcess ( processHandle, 0, output returnValue ).
      115         run CloseHandle ( processHandle, output returnValue ).
      116       end.
      117   
      118   end.
