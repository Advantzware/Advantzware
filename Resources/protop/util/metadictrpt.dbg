        1   /* metadictrpt.p
        2    *
        3    * simple, concise dictionary report
        4    *
        5    * a variant of util/dictrpt.p that only reports on the metaschema and VST tables
        6    *
        7    * december 9, 1988 */
        8   
        9   define new global shared variable pt_rptdir      as character no-undo initial ".".
       10   define new global shared variable pt_shortname   as character no-undo.
       11   
       12   define variable i          as integer   no-undo.
       13   define variable idx-member as character no-undo.
       14   define variable rptOut     as character no-undo.
       15   
       16   form header
       17        "Table                           Indexes Field                           Order  Type         Extent   Format           " skip
       18        "----------------------------------------------------------------------------------------------------------------------" skip
       19     with frame page-header
       20          no-labels
       21          page-top
       22          width 132
       23          no-attr-space.
       24   
       25   form _file._file-name   format "x(30)"
       26        idx-member
       27        _field._field-name format "x(30)"
       28        _field._order      space( 4 )
       29        _field._data-type
       30        _field._extent     space( 8 )
       31        _field._format
       32     with frame field-list
       33          down
       34          no-box
       35          no-labels
       36          width 132
       37          no-attr-space.
       38   
       39   rptOut = substitute( "&1/&2.metadict.rpt", pt_rptdir, max( pt_shortname, ldbname(1))).
       40   
       41   output to value( rptOut ) page-size 80.
       42   
       43   view frame page-header.
       44   
       45   for each _file no-lock where
       46         _file._file-name begins "_"
       47       by _file._file-name:
       48   
       49     for each _field no-lock where
       50           _field._file-recid = recid( _file )
       51         break by _field._file-recid by _field._order:
       52   
       53       if ( line-counter > page-size ) then page.
       54       if ( first-of( _field._file-recid ) and ( line-counter > ( page-size - 4 ))) then page.  /* don't start a table unless we can print a few lines.... */
       55   
       56       if ( first-of( _field._file-recid ) or
       57            (( line-counter > ( page-size - 4 )) and ( first-of( _field._file-recid ))) or
       58            ( line-counter <= 1 )) then display _file._file-name with frame field-list.
       59   
       60       /* index membership of the field    */
       61   
       62       i = 0.
       63       idx-member = "".
       64       for each _index where _index._file-recid = recid( _file ) no-lock:
       65         for each _index-field where _index-field._index-recid = recid( _index ) and _index-field._field-recid = recid( _field ) no-lock:
       66           if _index._unique then
       67             if recid( _index ) = _file._prime-index then
       68               substring( idx-member, 8 - i, 1 ) = "U".
       69              else
       70               substring( idx-member, 8 - i, 1 ) = "u".
       71            else
       72             if recid( _index ) = _file._prime-index then
       73               substring( idx-member, 8 - i, 1 ) = "+".
       74              else
       75               substring( idx-member, 8 - i, 1 ) = "*".
       76         end.
       77         i = i + 1.
       78         if i = 8 then leave.
       79       end.
       80   
       81       display idx-member                  /* display field attributes      */
       82               _field._field-name
       83               _field._order
       84               _field._data-type
       85               _field._extent
       86               _field._format
       87         with frame field-list.
       88   
       89       down 1 with frame field-list.
       90   
       91     end.
       92   
       93     down 1 with frame field-list.         /* a blank line between tables  */
       94   
       95   end.
       96   
       97   hide frame page-header.
       98   page.
       99   
      100   output close.
      101   
      102   /** message "Output is in" rptOut view-as alert-box. **/
      103   
      104   return.
