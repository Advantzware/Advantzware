        1   /* stinfo.p
        2    *
        3    * output assumes that your window is at least 132 columns wide...
        4    *
        5    * pro -p stinfo.p -param /db/dbname
        6    *
        7    *
        8    * prostrct statistics dbname
        9    * ...
       10    * 
       11    *   Statistics for Area: Schema Area
       12    * 
       13    *   Files in Area: Schema Area
       14    * ...
       15    * 
       16    *   Database Block Usage for Area: Schema Area
       17    * 
       18    *   Active blocks: 22618
       19    *     Data blocks: 11437
       20    *     Free blocks: 11181
       21    *    Empty blocks: 6
       22    *    Total blocks: 22624
       23    *   Extent blocks: 4
       24    *   Records/Block: 64
       25    *    Cluster size: 1
       26    *  ...
       27    * 
       28    */
       29   
       30   define variable lineIn as character no-undo.
       31   
       32   define variable areaName   as character no-undo format "x(30)" label "Area Name".
       33   define variable areaRPB    as integer   no-undo format   ">>9" label "RPB".
       34   define variable areaCSZ    as integer   no-undo format   ">>9" label "CSZ".
       35   define variable areaActive as int64     no-undo format ">,>>>,>>>,>>>,>>9" label "Active".
       36   define variable areaData   as int64     no-undo format ">,>>>,>>>,>>>,>>9" label "Data".
       37   define variable areaFree   as int64     no-undo format ">,>>>,>>>,>>>,>>9" label "Free".
       38   define variable areaEmpty  as int64     no-undo format ">,>>>,>>>,>>>,>>9" label "Empty".
       39   define variable areaTotal  as int64     no-undo format ">,>>>,>>>,>>>,>>9" label "Total".
       40   
       41   input through value( "prostrct statistics " + session:parameter ).
       42   repeat:
       43   
       44     import unformatted lineIn.
       45   
       46     lineIn = trim( lineIn ).
       47   
       48     if lineIn begins "Statistics for Area:" then
       49       assign
       50         areaName   = trim( entry( 2, lineIn, ":" ))
       51         areaRPB    = 0
       52         areaCSZ    = 0
       53         areaActive = 0
       54         areaData   = 0
       55         areaFree   = 0
       56         areaEmpty  = 0
       57         areaTotal  = 0
       58       .
       59   
       60     if lineIn begins "Active blocks:" then areaActive =   int64( trim( entry( 2, lineIn, ":" ))).
       61     if lineIn begins   "Data blocks:" then areaData   =   int64( trim( entry( 2, lineIn, ":" ))).
       62     if lineIn begins   "Free blocks:" then areaFree   =   int64( trim( entry( 2, lineIn, ":" ))).
       63     if lineIn begins  "Empty blocks:" then areaEmpty  =   int64( trim( entry( 2, lineIn, ":" ))).
       64     if lineIn begins  "Total blocks:" then areaTotal  =   int64( trim( entry( 2, lineIn, ":" ))).
       65     if lineIn begins "Records/Block:" then areaRPB    = integer( trim( entry( 2, lineIn, ":" ))).
       66     if lineIn begins  "Cluster size:" then areaCSZ    = integer( trim( entry( 2, lineIn, ":" ))).
       67   
       68     if lineIn begins "Cluster size:" /* and areaName matches "*idx*" */ then
       69       do:
       70         display
       71           areaName
       72           areaRPB
       73           areaCSZ
       74           areaActive
       75           areaData
       76           areaFree
       77           areaEmpty
       78           areaTotal
       79         .
       80       end.
       81   
       82   end.
       83   
       84   pause.
       85   
       86   return.
